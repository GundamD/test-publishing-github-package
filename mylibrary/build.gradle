plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'  // 1. Apply plugin
}

def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties"))) //Set env variable GPR_USER & GPR_API_KEY if not adding a properties file


// 2. Declare library details
ext {
    versionMajor = 1
    versionMinor = 0
    versionPatch = 3
    mGroupId = "com.gundamd.mylibrary"
    mArtifactId = "test-publishing-github-package"
    mVersionCode = 1
    mVersionName = generateVersionName(versionMajor, versionMinor, versionPatch)
    mLibraryName = "MyLibrary"
    mLibraryDescription = "My Library for test"
}

def static generateVersionCode() {
    return (System.currentTimeMillis() / 1000).toInteger()
}

def static generateVersionName(versionMajor, versionMinor, versionPatch) {
    String versionName = "${versionMajor}.${versionMinor}.${versionPatch}"
    return "${versionName}"
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
        versionCode mVersionCode
        versionName mVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
// 3. Declare task for creation of android sources.
task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

// 4. Make configuration for publishing artifact.
afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId mGroupId
                artifactId mArtifactId
                version mVersionName

                from components.release

                artifact androidSourcesJar

                pom {
                    name = mLibraryName
                    description = mLibraryDescription
                }
            }
        }

        // 5. Update repository details and credentials.
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/GundamD/test-publishing-github-package")
                credentials {
                    username = localProperties['gpr.usr'] ?: System.getenv("GPR_USER")
                    password = localProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
                }
            }
        }
    }
}

// Assembling should be performed before publishing package
publish.dependsOn assemble